/*
 * Copyright 2016 California Institute of Technology ("Caltech").
 * U.S. Government sponsorship acknowledged.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 * License Terms
 */
module jpl.imce.GenerateOntologicalModelingLanguage

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	bean = StandaloneSetup {
		scanClassPath  = true
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/XMLNamespace.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.emf.ecore/model/XMLType.genmodel"
		registerGeneratedEPackage = "org.eclipse.xtext.xbase.XbasePackage"
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.xbase/model/Xbase.genmodel"
		registerGenModelFile = "platform:/resource/org.eclipse.xtext.common.types/model/JavaVMTypes.genmodel"
	}
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "jpl.imce.oml.specification"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				ideaPlugin = {
					enabled = true
				}
				web = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * Copyright 2016 California Institute of Technology (\\\"Caltech\\\").\n * U.S. Government sponsorship acknowledged.\n *\n * Licensed under the Apache License, Version 2.0 (the \\\"License\\\");\n * you may not use this file except in compliance with the License.\n * You may obtain a copy of the License at\n *\n *     http://www.apache.org/licenses/LICENSE-2.0\n *\n * Unless required by applicable law or agreed to in writing, software\n * distributed under the License is distributed on an \\\"AS IS\\\" BASIS,\n * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n * See the License for the specific language governing permissions and\n * limitations under the License.\n */"
			}
		}
		language = StandardLanguage {
			name = "jpl.imce.oml.specification.OntologicalModelingLanguage"
			fileExtensions = "oml"
			referencedResource = "platform:/resource/jpl.imce.oml.specification.ecore/model/OMLCommon.xcore"
			referencedResource = "platform:/resource/jpl.imce.oml.specification.ecore/model/OMLTerminologies.xcore"
			referencedResource = "platform:/resource/jpl.imce.oml.specification.ecore/model/OMLGraphs.xcore"
			referencedResource = "platform:/resource/jpl.imce.oml.specification.ecore/model/OMLBundles.xcore"
			referencedResource = "platform:/resource/jpl.imce.oml.specification.ecore/model/OMLDescriptions.xcore"
				
			grammarUri = "platform:/resource/jpl.imce.oml.specification/src/jpl/imce/oml/specification/OntologicalModelingLanguage.xtext"
			
			//fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			//fragment = org.eclipse.xtext.generator.adapter.FragmentAdapter {
			//	fragment = org.eclipse.xtext.generator.ecore2xtext.FormatterFragment {}
			//}
			
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			
			serializer = {
				generateStub = false
			}
			
			validator = {
				composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
		}
	}
}
